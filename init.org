: Emacs Config
#+INFOJS_OPT: view:t toc:t ltoc:t mouse:underline buttons:0 path:https://www.linux.org.ru/tango/combined.css
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="http://www.pirilampo.org/styles/readtheorg/css/htmlize.css"/>
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="http://www.pirilampo.org/styles/readtheorg/css/readtheorg.css"/>

* Package system
** Initialization
   #+BEGIN_SRC emacs-lisp :tangle yes
     (require 'package)
     (setq package-archives
           `(,@package-archives
             ("melpa" . "https://melpa.org/packages/")
             ;; ("marmalade" . "https://marmalade-repo.org/packages/")
             ("org" . "https://orgmode.org/elpa/")
             ;; ("user42" . "https://download.tuxfamily.org/user42/elpa/packages/")
             ;; ("emacswiki" . "https://mirrors.tuna.tsinghua.edu.cn/elpa/emacswiki/")
             ;; ("sunrise" . "http://joseito.republika.pl/sunrise-commander/")
             ))
     (package-initialize)
   #+END_SRC
** [[https://github.com/jwiegley/use-package][Use-package]] installation and setup
   It can't install itself so we have to bootstrap it
   #+BEGIN_SRC emacs-lisp :tangle yes
     (setq package-enable-at-startup nil)

     ;; Bootstrap `use-package'
     (unless (package-installed-p 'use-package)
       (package-refresh-contents)
       (package-install 'use-package))

     (eval-when-compile
       (require 'use-package))

     (put 'use-package 'lisp-indent-function 1)
     (setq use-package-always-ensure t)
   #+END_SRC

   Use-package has a lot of useful keywords
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package system-packages
       :custom
       (system-packages-noconfirm t))

     (use-package use-package-ensure-system-package)

     ;; :diminish keyword
     (use-package diminish)

     ;; :bind keyword
     (use-package bind-key)

     ;; :quelpa keyword
     (use-package quelpa)
     (use-package quelpa-use-package)

     (use-package use-package-secrets
       :ensure nil
       :custom
       (use-package-secrets-default-directory "~/.emacs.d/secrets")
       :quelpa
       (use-package-secrets :repo "a13/use-package-secrets" :fetcher github :version original))

   #+END_SRC

** [[https://github.com/Malabarba/paradox][Modernized Package Menu]]
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package paradox
       :config
       (paradox-enable))
   #+END_SRC

* Global defaults

** Common
   These were defined in C code, so use emacs pseudo-package to set them.
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package emacs
       :ensure nil
       :init
       (put 'narrow-to-region 'disabled nil)
       (put 'downcase-region 'disabled nil)
       :custom
       (scroll-step 1)
       (inhibit-startup-screen t "Don't show splash screen")
       (use-dialog-box nil "Disable dialog boxes")
       (enable-recursive-minibuffers t "Allow minibuffer commands in the minibuffer")
       (indent-tabs-mode nil "Spaces!")
       (debug-on-quit nil))
   #+END_SRC

** Files

   Saving
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package files
       :ensure nil
       :hook
       (before-save . delete-trailing-whitespace)
       :custom
       (require-final-newline t)
       ;; backup settings
       (backup-by-copying t)
       ;; (backup-directory-alist
       ;;  '(("." . "~/.cache/emacs/backups")))
       (delete-old-versions t)
       (kept-new-versions 6)
       (kept-old-versions 2)
       (version-control t))

     (use-package autorevert
       :ensure nil
       :diminish auto-revert-mode)
   #+END_SRC

   [[https://github.com/a13/iqa.el][Quick access to init files]]
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package iqa
       :custom
       (iqa-user-init-file (concat user-emacs-directory "init.org") "Edit init.org by default.")
       :config
       (iqa-setup-default))
   #+END_SRC

   I don't use [[http://www.gnu.org/software/emacs/manual/html_node/emacs/Easy-Customization.html][Customize]], so put [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Saving-Customizations.html][custom-file]] "nowhere"
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package cus-edit
       :ensure nil
       :custom
       ;; alternatively, one can use `(make-temp-file "emacs-custom")'
       (custom-file null-device "Don't store customizations"))
   #+END_SRC

   Somehow file-related too
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package epa
       :ensure nil
       :custom
       (epg-gpg-program "gpg")
       (epa-pinentry-mode nil))

     (use-package uniquify
       :ensure nil
       :custom
       (uniquify-buffer-name-style 'forward))
   #+END_SRC

** Tramp
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package tramp
       :ensure nil
       :custom
       (tramp-backup-directory-alist backup-directory-alist)
       (tramp-default-method "ssh")
       (tramp-default-proxies-alist nil))

     (use-package sudo-edit
       :bind (:map ctl-x-map
                   ("M-s" . sudo-edit)))

   #+END_SRC
** Uncategorized
   (Mostly) keybindings customization
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package frame
       :ensure nil
       ;; disable suspending on C-z
       :bind
       ("C-z" . nil))

     (use-package delsel
       :ensure nil
       ;; C-c C-g always quits minubuffer
       :bind
       (:map mode-specific-map
             ("C-g" . minibuffer-keyboard-quit)))

     (use-package simple
       :ensure nil
       :diminish
       ((visual-line-mode . " â†©")
        (auto-fill-function . " â†µ"))
       :config
       (column-number-mode t)
       (toggle-truncate-lines 1)
       :bind
       ;; remap ctrl-w/ctrl-h
       (("C-w" . backward-kill-word)
        ("C-h" . delete-backward-char)
        :map ctl-x-map
        ("C-k" . kill-region)
        :map mode-specific-map
        ("h" . help-command)))

     (use-package ibuffer
       :ensure nil
       :bind
       ([remap list-buffers] . ibuffer))
   #+END_SRC

   [[https://github.com/purcell/exec-path-from-shell][Get environment variables from the shell]]
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package exec-path-from-shell
       :config
       (exec-path-from-shell-initialize))
   #+END_SRC
* Eshell
  #+BEGIN_SRC emacs-lisp :tangle yes
    (use-package eshell
      :ensure nil)

    (use-package em-smart
      :ensure nil
      :config
      (eshell-smart-initialize)
      :custom
      (eshell-where-to-jump 'begin)
      (eshell-review-quick-commands nil)
      (eshell-smart-space-goes-to-end t))

    (use-package esh-autosuggest
      :hook (eshell-mode . esh-autosuggest-mode)
      :ensure t)

    (use-package eshell-toggle
      :ensure nil
      :quelpa
      (eshell-toggle :repo "4DA/eshell-toggle" :fetcher github :version original)
      :bind
      (("M-`" . eshell-toggle)))

    (use-package eshell-fringe-status
      :config
      (add-hook 'eshell-mode-hook 'eshell-fringe-status-mode))
  #+END_SRC
* Dired
  #+BEGIN_SRC emacs-lisp :tangle yes
    (use-package ls-lisp
      :ensure nil
      :custom
      (ls-lisp-emulation 'MS-Windows)
      (ls-lisp-ignore-case t)
      (ls-lisp-verbosity nil))

    (use-package dired
      :ensure nil
      :custom (dired-dwim-target t "guess a target directory")
      :hook
      (dired-mode . dired-hide-details-mode))

    (use-package dired-x
      :ensure nil
      :bind
      ([remap list-directory] . dired-jump)
      :custom
      ;; do not bind C-x C-j since it's used by jabber.el
      (dired-bind-jump nil))

    (use-package dired-toggle)

    (use-package dired-hide-dotfiles
      :bind
      (:map dired-mode-map
            ("." . dired-hide-dotfiles-mode))
      :hook
      (dired-mode . dired-hide-dotfiles-mode))

    (use-package diredfl
      :hook
      (dired-mode . diredfl-mode))

    (use-package dired-launch)
  #+END_SRC

* Interface

** Localization
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package mule
       :ensure nil
       :config
       (prefer-coding-system 'utf-8)
       (set-terminal-coding-system 'utf-8)
       (set-language-environment "UTF-8"))

     (use-package ispell
       :ensure nil
       :custom
       (ispell-local-dictionary-alist
        '(("russian"
           "[ÐÐ‘Ð’Ð“Ð”Ð•ÐÐ–Ð—Ð˜Ð™ÐšÐ›ÐœÐÐžÐŸÐ Ð¡Ð¢Ð£Ð¤Ð¥Ð¦Ð§Ð¨Ð©Ð¬Ð«ÐªÐ­Ð®Ð¯Ð°Ð±Ð²Ð³Ð´ÐµÑ‘Ð¶Ð·Ð¸Ð¹ÐºÐ»Ð¼Ð½Ð¾Ð¿Ñ€ÑÑ‚ÑƒÑ„Ñ…Ñ†Ñ‡ÑˆÑ‰ÑŒÑ‹ÑŠÑÑŽÑÑ–Ñ—Ñ”Ò‘â€™A-Za-z]"
           "[^ÐÐ‘Ð’Ð“Ð”Ð•ÐÐ–Ð—Ð˜Ð™ÐšÐ›ÐœÐÐžÐŸÐ Ð¡Ð¢Ð£Ð¤Ð¥Ð¦Ð§Ð¨Ð©Ð¬Ð«ÐªÐ­Ð®Ð¯Ð°Ð±Ð²Ð³Ð´ÐµÑ‘Ð¶Ð·Ð¸Ð¹ÐºÐ»Ð¼Ð½Ð¾Ð¿Ñ€ÑÑ‚ÑƒÑ„Ñ…Ñ†Ñ‡ÑˆÑ‰ÑŒÑ‹ÑŠÑÑŽÑÑ–Ñ—Ñ”Ò‘â€™A-Za-z]"
           "[-']"  nil ("-d" "uk_UA,ru_RU,en_US") nil utf-8)))
       (ispell-program-name "hunspell")
       (ispell-dictionary "russian")
       (ispell-really-aspell nil)
       (ispell-really-hunspell t)
       (ispell-encoding8-command t)
       (ispell-silently-savep t))

     (use-package flyspell
       :ensure nil
       :custom
       (flyspell-delay 1))
   #+END_SRC

** Fonts
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package faces
       :ensure nil
       :defer t
       :custom
       (face-font-family-alternatives '(("Consolas" "Monaco" "Monospace")))
       :config
       (set-face-attribute 'default
                           nil
                           :family (caar face-font-family-alternatives)
                           :weight 'regular
                           :width 'semi-condensed
                           ;; (/ (* 19 (display-pixel-height)) (display-mm-height))
                           :height (if (> (display-pixel-height) 1000) 160 120))
       (set-fontset-font "fontset-default" 'cyrillic
                         (font-spec :registry "iso10646-1" :script 'cyrillic)))
   #+END_SRC

** Colors
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package custom
       :ensure nil
       :custom
       (custom-safe-themes t))

     (use-package gruvbox-theme
       :config
       (load-theme 'gruvbox-dark-medium))
   #+END_SRC

** GUI
   Disable gui elements we don't need:
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package tool-bar
       :ensure nil
       :config
       (tool-bar-mode -1))

     (use-package scroll-bar
       :ensure nil
       :config
       (scroll-bar-mode -1))

     (use-package menu-bar
       :ensure nil
       :config
       (menu-bar-mode -1)
       :bind
       ([S-f10] . menu-bar-mode))
   #+END_SRC

   Add those we want instead
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package time
       :ensure nil
       :custom
       (display-time-default-load-average nil)
       (display-time-24hr-format t)
       :config
       (display-time-mode t))

     (use-package fancy-battery
       :hook
       (after-init . fancy-battery-mode))

     (use-package yahoo-weather
       :bind (:map mode-specific-map
                   ("w" . yahoo-weather-mode))
       :custom
       ;; TODO: autolocate
       (yahoo-weather-location "Kyiv, UA"))

   #+END_SRC

   Fancy stuff
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package spaceline
       :config
       (require 'spaceline-config)
       (spaceline-spacemacs-theme))

     (use-package font-lock+
       :ensure t
       :quelpa
       (font-lock+ :repo "emacsmirror/font-lock-plus" :fetcher github))

     (use-package all-the-icons
       :config
       (add-to-list
        'all-the-icons-mode-icon-alist
        '(package-menu-mode all-the-icons-octicon "package" :v-adjust 0.0)))

     (use-package all-the-icons-dired
       :hook
       (dired-mode . all-the-icons-dired-mode))

     (use-package spaceline-all-the-icons
       :after spaceline
       :config
       (spaceline-all-the-icons-theme)
       (spaceline-all-the-icons--setup-package-updates)
       (spaceline-all-the-icons--setup-git-ahead)
       (spaceline-all-the-icons--setup-paradox))

     (use-package all-the-icons-ivy
       :after ivy projectile
       :custom
       (all-the-icons-ivy-buffer-commands '() "Don't use for buffers.")
       (all-the-icons-ivy-file-commands
        '(counsel-find-file
          counsel-file-jump
          counsel-recentf
          counsel-projectile-find-file
          counsel-projectile-find-dir) "Prettify more commands.")
       :config
       (all-the-icons-ivy-setup))

   #+END_SRC

   Dashboard
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package dashboard
       :config
       (dashboard-setup-startup-hook)
       :custom
       (initial-buffer-choice '(lambda ()
                                 (setq initial-buffer-choice nil)
                                 (get-buffer "*dashboard*")))
       (dashboard-items '((recents  . 5)
                          (bookmarks . 5)
                          (projects . 5)
                          ;; (agenda . 5)
                          (registers . 5))))
   #+END_SRC

   And the winner isâ€¦
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package winner
       :ensure nil
       :config
       (winner-mode 1))
   #+END_SRC
** Highlighting
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package paren
       :ensure nil
       :config
       (show-paren-mode t))

     (use-package hl-line
       :ensure nil
       :config
       (global-hl-line-mode 1))

     (use-package page-break-lines
       :config
       (global-page-break-lines-mode))

     (use-package rainbow-delimiters
       :hook
       (prog-mode . rainbow-delimiters-mode))

     (use-package rainbow-identifiers
       :hook
       (prog-mode . rainbow-identifiers-mode))

     (use-package rainbow-mode
       :diminish rainbow-mode
       :hook prog-mode)
   #+END_SRC

* Minibuffer (search, commands)
  I prefer [[https://github.com/abo-abo/swiper][Ivy/Counsel/Swiper]] over Helm
  #+BEGIN_SRC emacs-lisp :tangle yes
    ;; counsel-M-x can use this one
    (use-package smex)

    (use-package ivy
      :diminish ivy-mode
      :custom
      ;; (ivy-re-builders-alist '((t . ivy--regex-fuzzy)))
      (ivy-count-format "%d/%d " "Show anzu-like counter")
      (ivy-use-selectable-prompt t "Make the prompt line selectable")
      :custom-face
      (ivy-current-match ((t (:inherit 'hl-line))))
      :bind
      (:map mode-specific-map
            ("C-r" . ivy-resume))
      :config
      (ivy-mode t))

    (use-package ivy-xref
      :custom
      (xref-show-xrefs-function #'ivy-xref-show-xrefs "Use Ivy to show xrefs"))

    (use-package counsel
      :bind
      (([remap menu-bar-open] . counsel-tmm)
       ([remap insert-char] . counsel-unicode-char)
       ([remap isearch-forward] . counsel-grep-or-swiper)
       :map mode-specific-map
       :prefix-map counsel-prefix-map
       :prefix "c"
       ("a" . counsel-apropos)
       ("b" . counsel-bookmark)
       ("d" . counsel-dired-jump)
       ("e" . counsel-expression-history)
       ("f" . counsel-file-jump)
       ("g" . counsel-org-goto)
       ("h" . counsel-command-history)
       ("i" . counsel-imenu)
       ("l" . counsel-locate)
       ("m" . counsel-mark-ring)
       ("o" . counsel-outline)
       ("p" . counsel-package)
       ("r" . counsel-recentf)
       ("s g" . counsel-grep)
       ("s r" . counsel-rg)
       ("s s" . counsel-ag)
       ("t" . counsel-org-tag)
       ("v" . counsel-set-variable)
       ("w" . counsel-wmctrl))
      :init
      (counsel-mode))

    (use-package swiper)

    (use-package counsel-extras
      :disabled t
      :ensure nil
      :quelpa
      (counsel-extras :repo "a13/counsel-extras" :fetcher github :version original)
      :bind
      (("s-p" . counsel-extras-xmms2-jump)))

    (use-package ivy-rich
      :custom
      (ivy-rich-switch-buffer-name-max-length 60 "Increase max length of buffer name.")
      :config
      (ivy-rich-mode 1))
  #+END_SRC
  Usually, I don't use isearch, but just in case
  #+BEGIN_SRC emacs-lisp :tangle yes
    (use-package isearch
      :ensure nil
      :bind
      ;; TODO: maybe get a keybinding from global map
      (:map isearch-mode-map
            ("C-h" . isearch-delete-char)))
  #+END_SRC
* Quick jumps
  Ace-jump is dead, long live [[https://github.com/abo-abo/avy][Avy]]!
  #+BEGIN_SRC emacs-lisp :tangle yes
    (use-package avy
      :config
      (avy-setup-default)
      :bind
      (("C-:" . avy-goto-char)
       ;; ("C-'" . avy-goto-char-2)
       ("M-g M-g" . avy-goto-line)
       ("M-s M-s" . avy-goto-word-1)))

    (use-package avy-zap
      :bind
      ([remap zap-to-char] . avy-zap-to-char))

    (use-package ace-jump-buffer
      :bind
      (("M-g b" . ace-jump-buffer)))

    (use-package ace-window
      :custom
      (aw-keys '(?a ?s ?d ?f ?g ?h ?j ?k ?l) "Use home row for selecting.")
      (aw-scope 'frame "Highlight only current frame.")
      :bind
      (("M-o" . ace-window)))

    (use-package link-hint
      :bind
      (("<XF86Search>" . link-hint-open-link)
       ("S-<XF86Search>" . link-hint-copy-link)
       :map mode-specific-map
       :prefix-map link-hint-keymap
       :prefix "l"
       ("o" . link-hint-open-link)
       ("c" . link-hint-copy-link)))

    (use-package ace-link
      :after link-hint ; to use prefix keymap
      :bind
      (:map link-hint-keymap
            ("l" . counsel-ace-link))
      :config
      (ace-link-setup-default))


  #+END_SRC

* Regions/Kill ring/Clipboard
  #+BEGIN_SRC emacs-lisp :tangle yes
    (use-package select
      :ensure nil
      :custom
      (x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING))
      (select-enable-clipboard t "Use the clipboard"))

    (use-package expand-region
      :bind
      ("C-=" . er/expand-region))

    (use-package edit-indirect
      :bind
      (:map mode-specific-map
            ("r" . edit-indirect-region)))

    (use-package clipmon
      :config
      (clipmon-mode))

    (use-package copy-as-format
      :custom
      (copy-as-format-default "github")
      :bind
      (:map mode-specific-map
            :prefix-map copy-as-format-prefix-map
            :prefix "f"
            ("f" . copy-as-format)
            ("a" . copy-as-format-asciidoc)
            ("b" . copy-as-format-bitbucket)
            ("d" . copy-as-format-disqus)
            ("g" . copy-as-format-github)
            ("l" . copy-as-format-gitlab)
            ("c" . copy-as-format-hipchat)
            ("h" . copy-as-format-html)
            ("j" . copy-as-format-jira)
            ("m" . copy-as-format-markdown)
            ("w" . copy-as-format-mediawiki)
            ("o" . copy-as-format-org-mode)
            ("p" . copy-as-format-pod)
            ("r" . copy-as-format-rst)
            ("s" . copy-as-format-slack)))
  #+END_SRC

* Help
  #+BEGIN_SRC emacs-lisp :tangle yes
    (use-package man
      :ensure nil
      :custom-face
      (Man-overstrike ((t (:inherit font-lock-type-face :bold t))))
      (Man-underline ((t (:inherit font-lock-keyword-face :underline t)))))

    (use-package keyfreq
      :config
      (keyfreq-mode 1)
      (keyfreq-autosave-mode 1))

    (use-package which-key
      :diminish which-key-mode
      :config
      (which-key-mode))

    (use-package free-keys
      :commands free-keys)

    (use-package helpful)
  #+END_SRC

* Internet
** Jabber
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package jabber
       :secret
       (jabber-connect-all "jabber.el.gpg")
       :config
       (setq jabber-history-enabled t
             jabber-use-global-history nil
             fsm-debug nil)
       (custom-set-variables
        '(jabber-auto-reconnect t)
        '(jabber-chat-buffer-format "*-jc-%n-*")
        '(jabber-groupchat-buffer-format "*-jg-%n-*")
        '(jabber-chat-foreign-prompt-format "â–¼ [%t] %n> ")
        '(jabber-chat-local-prompt-format "â–² [%t] %n> ")
        '(jabber-muc-colorize-foreign t)
        '(jabber-muc-private-buffer-format "*-jmuc-priv-%g-%n-*")
        '(jabber-rare-time-format "%e %b %Y %H:00")
        '(jabber-resource-line-format "   %r - %s [%p]")
        '(jabber-roster-buffer "*-jroster-*")
        '(jabber-roster-line-format "%c %-17n")
        '(jabber-roster-show-bindings nil)
        '(jabber-roster-show-title nil)
        '(jabber-roster-sort-functions (quote (jabber-roster-sort-by-status jabber-roster-sort-by-displayname jabber-roster-sort-by-group)))
        '(jabber-show-offline-contacts nil)
        '(jabber-show-resources nil)))

     (use-package jabber-otr)

     (use-package point-im
       :ensure nil
       :defines point-im-reply-id-add-plus
       :quelpa
       (point-im :repo "a13/point-im.el" :fetcher github :version original)
       :config
       (setq point-im-reply-id-add-plus nil)
       :hook
       (jabber-chat-mode . point-im-mode))

   #+END_SRC

** Slack
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package slack
       :secret
       (slack-start "work.el.gpg")
       :commands (slack-start)
       :custom
       (slack-buffer-emojify t) ;; if you want to enable emoji, default nil
       (slack-prefer-current-team t))

     ;; TODO: move somewhere
     (use-package alert
       :commands (alert)
       :custom
       (alert-default-style 'libnotify))

   #+END_SRC
** Web

   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package shr-color
       :ensure nil
       :custom
       (shr-color-visible-luminance-min 80 "Improve the contrast"))

     (use-package eww
       :ensure nil
       :custom
       (shr-use-fonts nil)
       (eww-search-prefix "https://duckduckgo.com/html/?kd=-1&q="))

     (use-package browse-url
       :ensure nil
       :bind
       ([f5] . browse-url)
       :config
       (setq browse-url-browser-function 'browse-url-generic
             browse-url-generic-program "x-www-browser")

       (defun feh-browse (url &rest ignore)
         "Browse image using feh."
         (interactive (browse-url-interactive-arg "URL: "))
         (start-process (concat "feh " url) nil "feh" url))

       (defun mpv-browse (url &rest ignore)
         "Browse video using mpv."
         (interactive (browse-url-interactive-arg "URL: "))
         (start-process (concat "mpv --loop-file=inf" url) nil "mpv" "--loop-file=inf" url))

       (defvar browse-url-images-re
         '("\\.\\(jpe?g\\|png\\)\\(:large\\|:orig\\)?\\(\\?.*\\)?$"
           "^https?://img-fotki\\.yandex\\.ru/get/"
           "^https?://pics\\.livejournal\\.com/.*/pic/"
           "^https?://l-userpic\\.livejournal\\.com/"
           "^https?://img\\.leprosorium\\.com/[0-9]+$")
         "Image URLs regular expressions list.")

       (defvar browse-url-videos-re
         '("\\.\\(gifv?\\|avi\\|AVI\\|mp[4g]\\|MP4\\|webm\\)$"
           "^https?://\\(www\\.youtube\\.com\\|youtu\\.be\\|coub\\.com\\|vimeo\\.com\\|www\\.liveleak\\.com\\)/"
           "^https?://www\\.facebook\\.com/.*/videos?/"))

       (setq browse-url-browser-function
             (append
              (mapcar (lambda (re)
                        (cons re #'eww-browse-url))
                      browse-url-images-re)
              (mapcar (lambda (re)
                        (cons re #'mpv-browse))
                      browse-url-videos-re)
              '(("." . browse-url-xdg-open)))))

     (use-package webjump
       :bind
       (([S-f5] . webjump))
       :config
       (setq webjump-sites
             (append '(("debian packages" .
                        [simple-query "packages.debian.org" "http://packages.debian.org/" ""]))
                     webjump-sample-sites)))

     (use-package atomic-chrome
       :custom
       (atomic-chrome-url-major-mode-alist
        '(("reddit\\.com" . markdown-mode)
          ("github\\.com" . gfm-mode)
          ("redmine" . textile-mode))
        "Major modes for URLs.")
       :config
       (atomic-chrome-start-server))

     (use-package shr-tag-pre-highlight
       :after shr
       :config
       (add-to-list 'shr-external-rendering-functions
                    '(pre . shr-tag-pre-highlight))

       (when (version< emacs-version "26")
         (with-eval-after-load 'eww
           (advice-add 'eww-display-html :around
                       'eww-display-html--override-shr-external-rendering-functions))))

     (use-package google-this
       :diminish google-this-mode
       :init
       (google-this-mode 1)
       :bind
       (:map mode-specific-map
             ("g" . google-this-mode-submap)))

     (use-package multitran)

   #+END_SRC

** E-mail
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package smtpmail
       :ensure nil
       ;; let's install it now, since mu4e packages aren't available yet
       :ensure-system-package (mu . mu4e)
       :config
       ;;set up queue for offline email
       ;;use mu mkdir  ~/Maildir/queue to set up first
       (setq smtpmail-queue-mail nil  ;; start in normal mode
             smtpmail-queue-dir "~/.mail/queue/cur"))

     (use-package mu4e-vars
       :load-path "/usr/share/emacs/site-lisp/mu4e"
       :ensure nil
       :config
       ;;location of my maildir
       ;; enable inline images
       (setq mu4e-view-show-images t)
       ;; use imagemagick, if available
       (when (fboundp 'imagemagick-register-types)
         (imagemagick-register-types))

       (setq mu4e-maildir (expand-file-name "~/.mail/work"))
       ;; ivy does all the work
       (setq mu4e-completing-read-function 'completing-read)

       ;;command used to get mail
       ;; use this for testing
       (setq mu4e-get-mail-command "true")
       ;; use this to sync with mbsync
       (setq mu4e-get-mail-command "mbsync work")
       ;;rename files when moving
       ;;NEEDED FOR MBSYNC
       (setq mu4e-change-filenames-when-moving t))

     (use-package mu4e-contrib
       :ensure nil
       :custom
       (mu4e-html2text-command 'mu4e-shr2text))
     (use-package mu4e-alert
       :after mu4e
       :init
       (mu4e-alert-set-default-style 'notifications)
       :hook ((after-init . mu4e-alert-enable-mode-line-display)
              (after-init . mu4e-alert-enable-notifications)))

     (use-package mu4e-maildirs-extension
       :after mu4e
       :defines mu4e-maildirs-extension-before-insert-maildir-hook
       :init
       (mu4e-maildirs-extension)
       :config
       ;; don't draw a newline
       (setq mu4e-maildirs-extension-before-insert-maildir-hook '()))
   #+END_SRC

* Org
  #+BEGIN_SRC emacs-lisp :tangle yes
    (use-package calendar
      :ensure nil
      :custom
      (calendar-week-start-day 1))

    (use-package org
      ;; to be sure we have latest Org version
      :ensure org-plus-contrib
      :custom
      (org-src-tab-acts-natively t))

    (use-package org-bullets
      :custom
      ;; org-bullets-bullet-list
      ;; default: "â—‰ â—‹ âœ¸ âœ¿"
      ;; large: â™¥ â— â—‡ âœš âœœ â˜¯ â—† â™  â™£ â™¦ â˜¢ â€ â—† â—– â–¶
      ;; Small: â–º â€¢ â˜… â–¸
      (org-bullets-bullet-list '("â€¢"))
      ;; others: â–¼, â†´, â¬Ž, â¤·,â€¦, and â‹±.
      ;; (org-ellipsis "â¤µ")
      (org-ellipsis "â€¦")
      :hook
      (org-mode . org-bullets-mode))

    (use-package htmlize
      :custom
      (org-html-htmlize-output-type 'css)
      (org-html-htmlize-font-prefix "org-"))

    (use-package org-password-manager
      :hook
      (org-mode . org-password-manager-key-bindings))

    (use-package org-jira
      :custom
      (jiralib-url "http://jira:8080"))

  #+END_SRC

* Programming-related

   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package ibuffer-vc
       :config
       (define-ibuffer-column icon
         (:name "Icon" :inline t)
         (all-the-icons-ivy--icon-for-mode major-mode))
       :custom
       (ibuffer-formats
        '((mark modified read-only vc-status-mini " "
                (name 18 18 :left :elide)
                " "
                (size 9 -1 :right)
                " "
                (mode 16 16 :left :elide)
                " "
                filename-and-process)) "include vc status info")
       :hook
       (ibuffer . (lambda ()
                    (ibuffer-vc-set-filter-groups-by-vc-root)
                    (unless (eq ibuffer-sorting-mode 'alphabetic)
                      (ibuffer-do-sort-by-alphabetic)))))

     (use-package gitconfig-mode)

     (use-package gitignore-mode)

     (use-package magit
       :custom
       (magit-completing-read-function 'ivy-completing-read "Force Ivy usage.")
       :bind
       (:map mode-specific-map
             :prefix-map magit-prefix-map
             :prefix "m"
             (("a" . magit-stage-file) ; the closest analog to git add
              ("b" . magit-blame)
              ("B" . magit-branch)
              ("c" . magit-checkout)
              ("C" . magit-commit)
              ("d" . magit-diff)
              ("D" . magit-discard)
              ("f" . magit-fetch)
              ("g" . vc-git-grep)
              ("G" . magit-gitignore)
              ("i" . magit-init)
              ("l" . magit-log)
              ("m" . magit)
              ("M" . magit-merge)
              ("n" . magit-notes-edit)
              ("p" . magit-pull)
              ("P" . magit-push)
              ("r" . magit-reset)
              ("R" . magit-rebase)
              ("s" . magit-status)
              ("S" . magit-stash)
              ("t" . magit-tag)
              ("T" . magit-tag-delete)
              ("u" . magit-unstage)
              ("U" . magit-update-index))))

     (use-package magithub
       :after magit
       :custom
       (magithub-clone-default-directory "~/git/")
       :config
       (magithub-feature-autoinject t))

     (use-package browse-at-remote
       :after link-hint
       :bind
       (:map link-hint-keymap
             ("r" . browse-at-remote)
             ("k" . browse-at-remote-kill)))

     (use-package smerge-mode
       :ensure nil
       :diminish smerge-mode)

     (use-package diff-hl
       :hook
       ((magit-post-refresh . diff-hl-magit-post-refresh)
        (prog-mode . diff-hl-mode)
        (org-mode . diff-hl-mode)
        (dired-mode . diff-hl-dired-mode)))

     (use-package smart-comment
       :bind ("M-;" . smart-comment))

     (use-package projectile
       :bind
       (:map mode-specific-map ("p" . projectile-command-map))
       :custom
       (projectile-project-root-files-functions
        '(projectile-root-local
          projectile-root-top-down
          projectile-root-bottom-up
          projectile-root-top-down-recurring))
       (projectile-completion-system 'ivy))

     (use-package counsel-projectile
       :after counsel projectile
       :config
       (counsel-projectile-mode))
   #+END_SRC
** Search
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package ag
       :ensure-system-package (ag . silversearcher-ag)
       :custom
       (ag-highlight-search t "Highlight the current search term."))

     (use-package dumb-jump
       :custom
       (dumb-jump-selector 'ivy)
       (dumb-jump-prefer-searcher 'ag))
   #+END_SRC
** Autocompletion

   [[http://company-mode.github.io/][Company]]. There's also Auto-Complete as an alternative.
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package company
       :diminish company-mode
       :hook
       (after-init . global-company-mode))

     (use-package company-quickhelp
       :custom
       (company-quickhelp-delay 3)
       :config
       (company-quickhelp-mode 1))

     (use-package company-shell
       :config
       (add-to-list 'company-backends 'company-shell))

     (use-package company-emoji
       ;; :ensure-system-package fonts-symbola
       :config
       (add-to-list 'company-backends 'company-emoji)
       (set-fontset-font t 'symbol
                         (font-spec :family
                                    (if (eq system-type 'darwin)
                                        "Apple Color Emoji"
                                      "Symbola"))
                         nil 'prepend))
   #+END_SRC

** Snippets
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package autoinsert
       :hook
       (find-file . auto-insert))

     (use-package yasnippet
       :diminish yas-minor-mode
       :custom
       (yas-prompt-functions '(yas-completing-prompt yas-ido-prompt))

       :config
       (yas-reload-all)
       :hook
       (prog-mode  . yas-minor-mode))
   #+END_SRC

** Checking/linting
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package flycheck
       :diminish flycheck-mode
       :hook
       (prog-mode . flycheck-mode))

     (use-package avy-flycheck
       :config
       (avy-flycheck-setup))
   #+END_SRC

** Languages support

   [[https://xkcd.com/297/][https://imgs.xkcd.com/comics/lisp_cycles.png]]

*** Emacs Lisp

    #+BEGIN_SRC emacs-lisp :tangle yes
      (use-package lisp
        :ensure nil
        :hook
        (after-save-hook . check-parens))

      (use-package eros
        :hook
        (emacs-lisp-mode-hook . eros-mode))

      (use-package suggest)

      (use-package ipretty
        :config
        (ipretty-mode 1))

      (use-package nameless
        :hook
        (emacs-lisp-mode .  nameless-mode)
        :custom
        (nameless-private-prefix t))

      ;; bind-key can't bind to keymaps
      (use-package erefactor)

      (use-package flycheck-package
        :after flycheck
        (flycheck-package-setup))
    #+END_SRC

*** Scheme
    #+BEGIN_SRC emacs-lisp :tangle yes

      (use-package geiser)
    #+END_SRC

*** Clojure

    #+BEGIN_SRC emacs-lisp :tangle yes
      (use-package clojure-mode)
      (use-package clojure-mode-extra-font-locking)
      (use-package clojure-snippets)
      (use-package cider
        :config
        ;; sadly, we can't use :diminish keyword here, yet
        (diminish 'cider-mode
                  '(:eval (format " ðŸ%s" (cider--modeline-info)))))

      (use-package kibit-helper)

    #+END_SRC

*** Common Lisp

    Disabled for nowâ€¦

    #+BEGIN_SRC emacs-lisp :tangle yes

      (use-package slime
        :disabled
        :config
        (setq inferior-lisp-program "/usr/bin/sbcl"
              lisp-indent-function 'common-lisp-indent-function
              slime-complete-symbol-function 'slime-fuzzy-complete-symbol
              slime-startup-animation nil)
        (slime-setup '(slime-fancy))
        (setq slime-net-coding-system 'utf-8-unix))

    #+END_SRC

*** Scala

    #+BEGIN_SRC emacs-lisp :tangle yes
      (use-package scala-mode)

      (use-package sbt-mode
        :commands sbt-start sbt-command
        :config
        ;; WORKAROUND: https://github.com/ensime/emacs-sbt-mode/issues/31
        ;; allows using SPACE when in the minibuffer
        (substitute-key-definition
         'minibuffer-complete-word
         'self-insert-command
         minibuffer-local-completion-map))

      (use-package ensime
        :bind (:map ensime-mode-map
                    ("C-x C-e" . ensime-inf-eval-region)))


    #+END_SRC

*** Lua

    #+BEGIN_SRC emacs-lisp :tangle yes
      (use-package lua-mode)

      (use-package fennel-mode
        :ensure nil
        :quelpa (fennel-mode :repo "technomancy/fennel-mode" :fetcher gitlab))
    #+END_SRC

*** JS

    #+BEGIN_SRC emacs-lisp :tangle yes
      (use-package conkeror-minor-mode
        :hook
        (js-mode . (lambda ()
                     (when (string-match "conkeror" (or (buffer-file-name) ""))
                       (conkeror-minor-mode 1)))))

      (use-package json-mode)

      (use-package graphql-mode
        :mode "\\.graphql\\'"
        :custom
        (graphql-url "http://localhost:8000/api/graphql/query"))

    #+END_SRC

*** Shell
    #+BEGIN_SRC emacs-lisp :tangle yes
      (use-package sh-script
        :ensure nil
        :mode (("zshecl" . sh-mode)
               ("\\.zsh\\'" . sh-mode))
        :custom
        ;; zsh
        (system-uses-terminfo nil))

    #+END_SRC
*** Configuration files
    #+BEGIN_SRC emacs-lisp :tangle yes
      (use-package apt-sources-list)

      (use-package ssh-config-mode
        :init
        (autoload 'ssh-config-mode "ssh-config-mode" t)
        :mode
        (("/\\.ssh/config\\'"     . ssh-config-mode)
         ("/sshd?_config\\'"      . ssh-config-mode)
         ("/known_hosts\\'"       . ssh-known-hosts-mode)
         ("/authorized_keys2?\\'" . ssh-authorized-keys-mode))
        :hook
        (ssh-config-mode . turn-on-font-lock))
    #+END_SRC

*** Markup
    #+BEGIN_SRC emacs-lisp :tangle yes
      (use-package markdown-mode
        :ensure-system-package markdown
        :mode (("\\`README\\.md\\'" . gfm-mode)
               ("\\.md\\'"          . markdown-mode)
               ("\\.markdown\\'"    . markdown-mode))
        :init (setq markdown-command "markdown"))
    #+END_SRC

*** HTTP
    #+BEGIN_SRC emacs-lisp :tangle yes
      (use-package restclient)

      (use-package ob-restclient)

      (use-package company-restclient
        :after (company restclient)
        :config
        (add-to-list 'company-backends 'company-restclient))

    #+END_SRC

* External utilities
** Network
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package net-utils
       :ensure-system-package traceroute
       :bind
       (:map mode-specific-map
             :prefix-map net-utils-prefix-map
             :prefix "n"
             ("p" . ping)
             ("i" . ifconfig)
             ("w" . iwconfig)
             ("n" . netstat)
             ("p" . ping)
             ("a" . arp)
             ("r" . route)
             ("h" . nslookup-host)
             ("d" . dig)
             ("s" . smbclient)
             ("t" . traceroute)))
   #+END_SRC
** Docker
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package docker
       :ensure t
       :bind
       (:map mode-specific-map
             ("d" . docker)))

     ;; not sure if these two should be here
     (use-package dockerfile-mode
       :mode "Dockerfile\\'")

     (use-package docker-compose-mode)
   #+END_SRC

** Misc
   [[https://github.com/syohex/emacs-emamux][Tmux interaction]]
   #+BEGIN_SRC emacs-lisp :tangle yes
     (use-package emamux)
   #+END_SRC

* [[https://github.com/a13/reverse-im.el][Reverse-im]]

  Shortcuts with non-English layout.
  #+BEGIN_SRC emacs-lisp :tangle yes
    (use-package reverse-im
      :config
      (add-to-list 'load-path "~/.xkb/contrib")
      (add-to-list 'reverse-im-modifiers 'super)
      (add-to-list 'reverse-im-input-methods
                   (if (require 'unipunct nil t)
                       "russian-unipunct"
                     "russian-computer"))
      (reverse-im-mode t))
  #+END_SRC

* The endâ€¦

  #+BEGIN_SRC emacs-lisp :tangle yes

    ;; Local Variables:
    ;; eval: (add-hook 'after-save-hook (lambda ()(org-babel-tangle)) nil t)
    ;; End:

  #+END_SRC
